plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'maven-publish'
}

group = 'com.maroontress'
version = '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation platform("org.junit:junit-bom:5.13.4")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = 11
    targetCompatibility = 11
}

compileTestJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = 17
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(false)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir('reports/jacoco-html'))
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.locale = 'en_US'
    options.encoding = 'UTF-8'
}

checkstyle {
    toolVersion = '11.0.0'
}

checkstyleMain {
    configFile = file("${rootDir}/config/checkstyle/main.xml")
}

checkstyleTest {
    configFile = file("${rootDir}/config/checkstyle/test.xml")
}

publishing {
    repositories {
        maven {
            name = 'LocalFile'
            url = uri("${localMavenRepo}")
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
